plugins {
    id "java-library"
    id "io.micronaut.build.internal.validation-base"
}

dependencies {
    implementation mn.logback.classic
    implementation mn.micronaut.inject.java
    implementation mn.micronaut.inject
    implementation mn.micronaut.aop
    implementation mn.micronaut.context

    implementation projects.validation
    implementation projects.validationProcessor
    implementation mnReactor.micronaut.reactor

    implementation 'org.jboss.arquillian.container:arquillian-container-test-spi:1.7.0.Alpha2'
    implementation libs.jakarta.validation.tck.tests
    testImplementation(libs.jakarta.validation.tck.tests) {
        artifact {
            classifier = "sources"
        }
    }

    testImplementation 'javax.annotation:javax.annotation-api:1.3.2'
}

test {
    scanForTestClasses false
    useTestNG {
        systemProperties = [
                'validation.provider': 'io.micronaut.validation.tck.runtime.TckValidationProvider'
        ]
        suites file("tck-tests.xml")
    }
}

tasks.register('singleTest', Test) {
    scanForTestClasses false
    useTestNG {
        suiteXmlBuilder().suite(name: 'Sample Suite') {
            test(name : 'Sample Test') {
                classes('') {
//                    'class'(name: 'org.hibernate.beanvalidation.tck.tests.methodvalidation.MethodValidationTest') {
//                        methods() {
//                            include(name: 'methodParameterValidationIncludesConstraintsFromImplementedInterface')
//                        }
//                    }
                    'class'(name: 'org.hibernate.beanvalidation.tck.tests.constraints.builtinconstraints.NotEmptyConstraintTest') {
                        methods() {
                            include(name: 'testNotEmptyConstraint')
                        }
                    }
                }
            }
        }
        systemProperties = [
                'validation.provider': 'io.micronaut.validation.tck.runtime.TckValidationProvider'
        ]
    }
}
